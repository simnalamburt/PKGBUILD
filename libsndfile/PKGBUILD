# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libsndfile
pkgver=1.0.31
pkgrel=1
pkgdesc="A C library for reading and writing files containing sampled sound"
arch=('x86_64')
url="https://libsndfile.github.io/libsndfile/"
license=('LGPL2.1')
depends=('glibc' 'libFLAC.so' 'libogg' 'libvorbis.so' 'libvorbisenc.so' 'opus')
makedepends=('alsa-lib')
optdepends=('alsa-lib: for sndfile-play')
provides=('libsndfile.so')

source=("https://github.com/$pkgname/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.bz2"{,.sig})
b2sums=('33e3c3d111427e829d8233aec52c227188834936839da9a04cf1ea5d2595d6a3127e7ce86d7686ca0336cca2017e6e2466845c27c7fc399efae34a0902316e41'
        'SKIP')
validpgpkeys=('31D95CAB6D80D262244A1750A47620E801E47E95') # David Seifert <soap@gentoo.org>"

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-sqlite
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
